apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode gitVersionCode()
        versionName gitVersionName()

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/commons-net-3.6.jar')
}

publish {
    userOrg = 'lamster2018' //bintray注册的用户名（不是邮箱）
    groupId = 'com.lahm.library'
    artifactId = 'okftp' //项目名称
    publishVersion = gitVersionName() //版本号
    desc = 'download/upload by ftp' //项目描述。可空。
    website = 'https://github.com/lamster2018/okftp' //项目地址，一般填github地址。可空。
}

/**
 * 通过git的Commit数来获取version code
 * @return
 */
def static gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
    10000 + cmd.execute().text.trim().toInteger()
}

/**
 * 通过git上的最近一个tag来获取version name
 * @return
 */
def static gitVersionName() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def arr = version.split("-")
    def name = arr[0].replace("alpha_", "").replace("_alpha", "")
    return name
}